apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.palantir.git-version'

version versionDetails().lastTag
group 'aga.android'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/migrations-release.aar")
            artifact sourceJar
            groupId 'aga.android'
            artifactId 'migrations'
            version this.version

            // Define this explicitly if using implementation or api configurations
            pom.withXml {
                def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key  = System.getenv('BINTRAY_API_KEY')
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'maven'
        name = 'migrations'
        description = "Database migration validations for Android projects"
        publish = true
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/agap/migrations.git'
        version {
            name = this.version
            desc = ""
            released = new Date()
            vcsTag = this.version
        }
    }
}

task publishMigrationsRelease {
    dependsOn 'clean'
    dependsOn 'assemble'
    dependsOn 'check'
    dependsOn 'bintrayUpload'

    tasks.findByName('assemble').mustRunAfter 'clean'
    tasks.findByName('check').mustRunAfter 'assemble'
    tasks.findByName('bintrayUpload').mustRunAfter 'check'
}